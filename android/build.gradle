plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.vanniktech.maven.publish'
    id 'signing'
}

android {
    compileSdk 33
    namespace "com.swmansion.starknet"

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                targets "crypto_jni", "poseidon_jni"
                arguments "-DCMAKE_BUILD_TYPE=Release", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
                cppFlags += "-Wno-error=unused-but-set-variable"
            }
        }

    }

    lint {
        baseline(file("lint-baseline.xml"))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "../crypto/CMakeLists.txt"
            version "3.18.1"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            kotlin.srcDirs = ['../lib/src/main']
        }
    }
}


dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.9.23'
}

// Get version and group from lib module
version = rootProject.project("lib").version
group = rootProject.project("lib").group

mavenPublishing {
    coordinates(group.toString(), "starknet", version.toString())

    pom {
        name.set("starknet")
        description.set("Starknet SDK for Android")
        url.set("https://github.com/software-mansion/starknet-jvm")
        licenses {
            license {
                name.set("MIT License")
                url.set("https://github.com/software-mansion/starknet-jvm/blob/main/LICENSE")
            }
        }
        developers {
            developer {
                id.set("jakubptak")
                name.set("Jakub Ptak")
                email.set("jakub.ptak@swmansion.com")
            }
            developer {
                id.set("arturmichalek")
                name.set("Artur Micha≈Çek")
                email.set("artur.michalek@swmansion.com")
            }
            developer {
                id.set("bartoszrybarski")
                name.set("Bartosz Rybarski")
                email.set("bartosz.rybarski@swmansion.com")
            }
            developer {
                id.set("wojciechszymczyk")
                name.set("Wojciech Szymczyk")
                email.set("wojciech.szymczyk@swmansion.com")
            }
            developer {
                id.set("maksimzdobnikau")
                name.set("Maksim Zdobnikau")
                email.set("maksim.zdobnikau@swmansion.com")
            }
            developer {
                id.set("franciszekjob")
                name.set("Franciszek Job")
                email.set("franciszek.job@swmansion.com")
            }
        }
        scm {
            connection.set("scm:git:git://github.com/software-mansion/starknet-jvm.git")
            developerConnection.set("scm:git:ssh://github.com:software-mansion/starknet-jvm.git")
            url.set("https://github.com/software-mansion/starknet-jvm/tree/main")
        }
    }
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()
}
